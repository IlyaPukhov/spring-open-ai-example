plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.puhovin'
version = '1.0.0'
description = 'spring-openai-example'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springAiVersion', '1.1.0')
}

dependencies {
    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Spring AI
    implementation 'org.springframework.ai:spring-ai-starter-model-openai'

    // Springdoc
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.8.14'

    // Micrometer
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }

    doFirst {
        def mockitoAgent = classpath.find { it.name.contains("byte-buddy-agent") }
        if (mockitoAgent) {
            jvmArgs "-javaagent:${mockitoAgent}"
        }
    }
}

sourceSets {
    test {
        java {
            srcDirs += 'src/testIntegration/java'
        }
        resources {
            srcDirs += 'src/testIntegration/resources'
        }
    }
}

jar.enabled = false